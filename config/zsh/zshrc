#!/usr/bin/env zsh
# vim:set ft=zsh sw=2 sts=2 ts=2 et:

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.config/dotfiles"
# Time format
export TIME="\t%e real,\t%U user,\t%S sys"

# your default editor
if (( $+commands[nvim] )); then
  export EDITOR='nvim'
  export VISUAL="nvim"
else
  export EDITOR='vi'
  export VISUAL="vi"
fi

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# all of our zsh files
typeset -U config_files
config_files=($ZDOTDIR/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load init files
for file in ${(M)config_files:#*/init.zsh}; do
  source "$file"
done

# load sheldon plugins
source <(sheldon source)

# load everything but the path and init files
for file in ${${config_files:#*/path.zsh}:#*/init.zsh}; do
  source "$file"
done

unset config_files

_screenfetch=($HOME/.screenfetch(Nmh-20))
if [[ $#_screenfetch < 1 ]] && [[ -x $(command -v screenfetch) ]]; then
  touch $HOME/.screenfetch
  screenfetch
fi

# Update environment before execute
function import_tmux_env() {
  if [[ -n "$TMUX" ]]; then
    ssh_auth_sock=$(tmux show-environment | grep "^SSH_AUTH_SOCK")
    [[ -n "$ssh_auth_sock" ]] && export $ssh_auth_sock
    display=$(tmux show-environment | grep  "^DISPLAY")
    [[ -n "$display" ]] && export $display
  fi
}
precmd_functions+=(import_tmux_env)

eval "$(starship init zsh)"

# start tmux on remote session
if [[ -n "$PS1" ]] && [[ -z "$TMUX" ]] && [[ -n "$SSH_CONNECTION" ]]; then
  tma -t ssh_tmux || tmn -s ssh_tmux
fi
