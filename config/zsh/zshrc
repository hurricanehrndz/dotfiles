#!/usr/bin/env zsh
# vim:set ft=zsh sw=2 sts=2 ts=2 et:

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.config/dotfiles"

# Antibody's home
export ANTIBODY_HOME="$HOME/.local/share/antibody"

# your project folder that we can `c [tab]` to
export PROJECTS="$HOME/Code"

if [[ ! $XDG_RUNTIME_DIR =~ "snap" ]]; then
  export SSH_AUTH_SOCK="$XDG_RUNTIME_DIR/gnupg/S.gpg-agent.ssh"
fi

# your default editor
if (( $+commands[nvim] )); then
  export EDITOR='nvim'
  export VISUAL="nvim"
else
  export EDITOR='vi'
  export VISUAL="vi"
fi
export VEDITOR='code'

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# all of our zsh files
typeset -U config_files
config_files=($ZDOTDIR/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load init files
for file in ${(M)config_files:#*/init.zsh}; do
  source "$file"
done

# load antibody plugins
if [[ -s "${ZDOTDIR}/zsh_plugins.sh" ]]; then
  source "${ZDOTDIR}/zsh_plugins.sh"
fi

# setup prompt
autoload -Uz promptinit && promptinit
prompt litespace

# load everything but the path and init files
for file in ${${config_files:#*/path.zsh}:#*/init.zsh}; do
  source "$file"
done

unset config_files

if (( ! $+functions[__vte_prompt_command] )); then
  VTE_PROFILE=( /etc/profile.d/vte*.sh(N) )
  if ([ $TILIX_ID ] || [ $VTE_VERSION ]) && [ $VTE_PROFILE ]; then
    source "$VTE_PROFILE[1]"
  fi
fi
